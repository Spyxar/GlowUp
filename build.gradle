plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${stonecutter.current.version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    //Cloth Config
    maven { url "https://maven.shedaniel.me/" }
    //ModMenu
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"
    mappings "net.fabricmc:yarn:${property('yarn_mappings')}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${property('fabric_version')}"

    modApi "com.terraformersmc:modmenu:${property('modmenu_version')}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${property('clothconfig_version')}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Toml
    modImplementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}")
    include "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft", stonecutter.current.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": stonecutter.current.version,
                "loader_version": project.loader_version
    }
}

loom {
    runConfigs.all {
        ideConfigGenerated true // Run configurations are not created for subprojects by default
        runDir "../../run" // Use a shared run folder and create separate worlds
    }
}

stonecutter {
    dependencies["modmenu"] = project.modmenu_version.toString()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    def java = stonecutter.eval(stonecutter.current.version, ">=1.20.5")
            ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
    targetCompatibility = java
    sourceCompatibility = java
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}